//file:noinspection UnnecessaryQualifiedReference
//file:noinspection GroovyAssignabilityCheck
plugins {
  id "fabric-loom" version "0.10.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "maven-publish"
}

String mod_version = loom.modVersion
String release_title = null
String changeLog = null

String mc_version = "21w42a"
String mc_version_major = "1.18"
String yarn_mappings = "1"        // https://fabricmc.net/versions.html
String loader_version = "0.12.2"  // https://fabricmc.net/versions.html
String fabric_version = "0.41.1"  // https://fabricmc.net/versions.html
String modmenu_version = "3.0.0"  // https://modrinth.com/mod/modmenu/versions

archivesBaseName = "critical-orientation"
version = mod_version + "+" + mc_version

repositories {
  maven {
    url "https://maven.terraformersmc.com"
    content { includeGroup "com.terraformersmc" }
  }
}

Set<String> apiModules = [
        "api-base",
        "lifecycle-events-v1",
        "resource-loader-v0",
        "key-binding-api-v1",
        "command-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${mc_version}"
  mappings "net.fabricmc:yarn:${mc_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  apiModules.forEach { modImplementation(fabricApi.module("fabric-$it", "${fabric_version}+${mc_version_major}")) }

  modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv().MODRINTH_TOKEN }
  token = System.getenv().MODRINTH_TOKEN
  projectId = "AFqV4ew3"
  versionType = "BETA"
  versionNumber = mod_version
  versionName = release_title
  changelog = changeLog
  uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")
  addGameVersion(mc_version)
}

java { withSourcesJar() }

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId = 'coffee.waffle'
      artifactId = archivesBaseName
      version = version
      artifact(remapJar) { builtBy remapJar }
      artifact(sourcesJar) { builtBy remapSourcesJar }
    }
  }
  repositories {
    maven {
      name = "emmavenReleases"
      url = "https://maven.waffle.coffee/releases"
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
    maven {
      name = "emmavenSnapshots"
      url = "https://maven.waffle.coffee/snapshots"
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
  }
}
