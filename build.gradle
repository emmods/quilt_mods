//file:noinspection UnnecessaryQualifiedReference
//file:noinspection GroovyAssignabilityCheck
plugins {
  id "fabric-loom" version "0.10.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "maven-publish"
}

boolean is_release = false
String mod_version = loom.modVersion
String release_title = null
String changeLog = null

String mc_version = "1.18.1"
String mc_version_major = "1.18"
String yarn_mappings = "22"       // https://fabricmc.net/develop
String loader_version = "0.12.12" // https://fabricmc.net/develop
String fabric_version = "0.46.2"  // https://fabricmc.net/develop
String modmenu_version = "3.0.0"  // https://modrinth.com/mod/modmenu/versions
String midnight_version = "0.3.1" // https://modrinth.com/mod/midnightlib/versions

archivesBaseName = "critical-orientation"
version = mod_version + "+" + mc_version + (!is_release ? "-SNAPSHOT" : "")
group = "coffee.waffle"

repositories {
  maven {
    url "https://maven.terraformersmc.com"
    content { includeGroup "com.terraformersmc" }
  }
  maven {
    url "https://api.modrinth.com/maven"
    content { includeGroup "maven.modrinth" }
  }
}

idea { module { downloadJavadoc = false } }

Set<String> apiModules = [
        "api-base",
        "lifecycle-events-v1",
        "resource-loader-v0",
        "key-binding-api-v1",
        "command-api-v1"
]

dependencies {
  minecraft "com.mojang:minecraft:${mc_version}"
  mappings "net.fabricmc:yarn:${mc_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  apiModules.forEach { modImplementation(fabricApi.module("fabric-$it", "${fabric_version}+${mc_version_major}")) }
  modRuntimeOnly(fabricApi.module("fabric-renderer-registries-v1", "${fabric_version}+${mc_version_major}"))

  modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
  modImplementation include("maven.modrinth:midnightlib:${midnight_version}")
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv().MODRINTH_TOKEN }
  token = System.getenv().MODRINTH_TOKEN
  projectId = "AFqV4ew3"
  versionType = "BETA"
  versionNumber = mod_version
  versionName = release_title
  changelog = changeLog
  uploadFile = remapJar
  addGameVersion(mc_version)
}

java { withSourcesJar() }

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
  repositories {
    maven {
      url = System.getenv().MAVEN_URL
      credentials {
        username = System.getenv().MAVEN_USER
        password = System.getenv().MAVEN_PASS
      }
      authentication { basic(BasicAuthentication) }
    }
  }
}
