plugins {
  alias libs.plugins.quilt.loom
  alias libs.plugins.quilt.licenser
  alias libs.plugins.minotaur
}

String releaseTitle = "Fix /align command"
String changeLog = null

group = "coffee.waffle"
archivesBaseName = "critical-orientation-" + libs.versions.minecraft.get()
version = loom.modVersion

repositories {
  maven {
    url "https://maven.terraformersmc.com"
    content { includeGroup "com.terraformersmc" }
  }
  maven {
    url "https://api.modrinth.com/maven"
    content { includeGroup "maven.modrinth" }
  }
}

dependencies {
  minecraft libs.minecraft
  mappings loom.layered {
    addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
  }
  modImplementation libs.quilt.loader
  modImplementation libs.quilted.fabric.api

  modImplementation libs.modmenu, {transitive false}

  modImplementation libs.midnightlib
  include libs.midnightlib
}

tasks.withType(JavaCompile).forEach {
  it.options.encoding = "UTF-8"
  it.options.release.set(17)
  it.sourceCompatibility = 17
}

java.withSourcesJar()
jar.from "LICENSE", "README.md"

license {
  rule "LICENSE"
  include "**/*.java"
}

modrinth {
  projectId = "orientation"
  versionName = releaseTitle
  changelog = changeLog
  uploadFile = remapJar
  additionalFiles = [sourcesJar]
  syncBodyFrom = project.file("README.md").text
  dependencies {
    required.project "qsl"
    optional.project "shadowedactionbar"
  }
}
tasks.modrinth.dependsOn tasks.modrinthSyncBody